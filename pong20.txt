1 REM TURBO BASIC XL NOW
10 GOSUB 1000 : REM INIT
20 IF F=1 THEN GR. 0: END
100 REM UPDATE BALL POSITION
105 NUMALIVE=0
108 DPOKE DL,S(SP):SP=NOT SP
109 DPOKE 88,S(SP): POKE 106,P(SP)+4: CLS
110 FOR I=1 TO N
120 IF A(I)=0 THEN 430
121 PP=PADDLE(0)/10: MOVE PY,PP,12: PY=PP
130 X(I)=X(I)+U(I): Y(I)=Y(I)+V(I): NUMALIVE=NUMALIVE+1
412 IF X(I)=0 THEN A(I)=0
414 IF X(I)=39 THEN U(I)=-1
416 IF Y(I)=0 THEN V(I)=1
418 IF Y(I)=23 THEN V(I)=-1
420 POKE S(SP)+X(I)+YY(Y(I)),160+I
430 NEXT I
440 IF NUMALIVE=0 THEN F=1
500 GOTO 20

999 REM INITIALIZE 
1000 RAMTOP = PEEK(106): POKE 106,RAMTOP-12
1001 GR. 0: SE.4,2,0: SE.2,2,14: SE.1,2,2: POKE 752,1 : TRAP 5000 : POKE 559,0
1010 SCORE=0: F=0
1020 N=26
1030 DIM X(N), Y(N), U(N), V(N), A(N)
1035 DIM YY(23)
1036 FOR I=0 to 23: YY(I)=I*40: NEXT I
1040 FOR I=1 TO N
1050 X(I)=INT(30*RND)+5
1060 Y(I)=INT(15*RND)+4
1070 U(I)=2*INT(2*RND)-1
1080 V(I)=INT(3*RND)-1
1090 A(I)=1
1100 NEXT I
1110 DIM S(1),P(2)
1120 DL = DPEEK(560)+4 
1130 P(0) = RAMTOP-8: P(1) = RAMTOP-4
1135 S(0) = P(0)*256+64: S(1) = P(1)*256+64: SP=0
1140 DPOKE 88,S(0): POKE 106,P(0)+4: CLS : DPOKE 88, S(1): POKE 106,P(1)+4: CLS
1200 REM SET UP PM GFX

1210 PMBASE=54279:REM                       Player-missile base pointer
1220 GRACTL=53277:REM                       CTIA graphics control register
1230 HPOSP0=53248:REM                       Horizontal position of P0
1260 PY=48: REM                               BASIC's player vertical position
1270 POKE PMBASE,RAMTOP-12:REM              Tell ANTIC where PM RAM is
1280 MYPMBASE=(RAMTOP-12)*256:REM                     Keep track of PM RAM address
1290 POKE 559,46:REM                     Enable PM DMA with 2-line res
1300 POKE GRACTL,3:REM                      Enable PM display
1310 POKE HPOSP0,45:REM                    Declare horizontal position
2100 FOR I=MYPMBASE+512 TO MYPMBASE+640:REM this loop clears player
2110 POKE I,0
2120 NEXT I
2130 FOR I=MYPMBASE+512+PY to MYPMBASE+523+PY
2150 POKE I,3
2160 NEXT I
2180 POKE 704,88:REM                     Make the player pink


2900 RETURN
5000 POKE 559,34 : END
