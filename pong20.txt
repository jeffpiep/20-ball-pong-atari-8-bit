1 REM TURBO BASIC XL NOW
10 GOSUB 1000 : REM INIT
20 IF F=1 THEN GR. 0: END
100 REM UPDATE BALL POSITION
105 NUMALIVE=0
108 DPOKE DL+4,S(SP):SP=NOT SP
109 DPOKE 88,S(SP): POKE 106,P(SP)+4: CLS
110 FOR I=1 TO N
120 IF A(I)=0 THEN 430
130 X(I)=X(I)+U(I): Y(I)=Y(I)+V(I): NUMALIVE=NUMALIVE+1
412 IF X(I)=1 THEN A(I)=0
414 IF X(I)=38 THEN U(I)=-1
416 IF Y(I)=1 THEN V(I)=1
418 IF Y(I)=22 THEN V(I)=-1
420 POKE S(SP)+X(I)+YY(Y(I)),160+I
430 NEXT I
440 IF NUMALIVE=0 THEN F=1
500 GOTO 20

999 REM INITIALIZE 
1000 RAMTOP = PEEK(106): POKE 106,RAMTOP-8
1001 GR. 0: POKE 752,1
1010 SCORE=0: F=0
1020 N=26
1030 DIM X(N), Y(N), U(N), V(N), A(N)
1035 DIM YY(23)
1036 FOR I=0 to 23: YY(I)=I*40: NEXT I
1040 FOR I=1 TO N
1050 X(I)=INT(30*RND(1))+5
1060 Y(I)=INT(15*RND(1))+4
1070 U(I)=2*INT(2*RND(1))-1
1080 V(I)=INT(3*RND(1))-1
1090 A(I)=1
1100 NEXT I
1110 DIM S(1),P(1)
1120 DL = DPEEK(560) 
1130 P(0) = RAMTOP-8: P(1) = RAMTOP-4
1135 S(0) = P(0)*256+64: S(1) = P(1)*256+64: SP=0
1140 DPOKE 88,S(0): POKE 106,P(0)+4: CLS : DPOKE 88, S(1): POKE 106,P(1)+4: CLS
1900 RETURN

